#! /bin/csh

set BACKUP_DIR=$HOME/backups

set observatory=$1
set server_mode=$2
set mirrored_server_url=$3

set TAIL="${observatory}.${server_mode}"

set database_backup_file=${BACKUP_DIR}/backup.${TAIL}.mysql 
set catalog_file=${BACKUP_DIR}/catalogs.${TAIL}.tar.gz
set deliveries_file=${BACKUP_DIR}/deliveries.${TAIL}.txt
set monitor_reprocessing_file=${BACKUP_DIR}/monitor_reprocessing.${TAIL}.tar.gz

echo "-----------------------------------------------------------"
echo "Shutting down cron activities."""
echo "-----------------------------------------------------------"
crontab </dev/null

echo "-----------------------------------------------------------"
echo "Moving server ${CRDS_PROJECT} ${CRDS_USECASE} to backup URL"
echo "-----------------------------------------------------------"
cd ${CRDS}/CRDS_server
tools/set_backup_url True
source env.csh

# This replaces the local database with the specified backup, presumably from the master server
echo "-----------------------------------------------------"
echo "Restoring database from $database_backup_file"
echo "-----------------------------------------------------"
cd ${CRDS}/CRDS_server
./manage dbrestore -f $database_backup_file

# This synchronizes the local cache to the master server
setenv CRDS_SERVER_URL $mirrored_server_url
echo "-----------------------------------------------------"
echo "Syncing mappings from $mirrored_server_url"
echo "-----------------------------------------------------"
cd ${CRDS}/CRDS_server
python -m crds.sync --all --repair-files --check-sha1sum --purge-mappings --verbose --stats

echo "-----------------------------------------------------"
echo "Fast-Syncing references from $mirrored_server_url"
echo "-----------------------------------------------------"
# setenv CRDS_DOWNLOAD_CHECKSUMS 0    # Omit sha1sum verification on initial download
cd ${CRDS}/CRDS_server
python -m crds.sync --all --repair-files --fetch-references --purge-references --purge-mappings --verbose --stats

# This checks the consistency of the database and server cache and delivery areas
echo "-----------------------------------------------------"
echo "Checking and purging orphan files."
echo "-----------------------------------------------------"
cd ${CRDS}/CRDS_server
tools/orphan_files #  --purge-delivery-errors

# This recreates the catalogs directory from a backup tarball.
echo "-----------------------------------------------------"
echo "Copying catalog files from backup"
echo "-----------------------------------------------------"
cd ${CRDS_PATH}/../server_files
rm -rf catalogs
tar zxvf $catalog_file

# This recreates the delivery directory with files from the database which are
# recorded as not-yet-delivered.
echo "-----------------------------------------------------"
echo "Recreating delivery directory and delivering files in state 'delivered'."
echo "-----------------------------------------------------"
cd ${CRDS_PATH}/../server_files
rm -rf deliveries
mkdir deliveries
cd ${CRDS}/CRDS_server
# tools/redeliver_catalogs --find-undelivered-catalogs
tools/redeliver_catalogs --files `cat $deliveries_file`

echo "-----------------------------------------------------"
echo "Rechecking orphan files."
echo "-----------------------------------------------------"
cd ${CRDS}/CRDS_server
tools/orphan_files

echo "-----------------------------------------------------"
echo "Copying monitor_reprocessing files from backup"
echo "-----------------------------------------------------"
if ( -e $monitor_reprocessing_file ) then
    cd ${CRDS}
    rm -rf monitor_reprocessing
    tar zxvf $monitor_reprocessing_file
else
    echo "No monitor_reprocessing backup file,  skipping."
endif

echo "-----------------------------------------------------"
echo "Re-installing and restarting server ${CRDS_PROJECT} ${CRDS_USECASE}"
echo "-----------------------------------------------------"
cd ${CRDS}/CRDS_server
tools/set_backup_url False

echo "-----------------------------------------------------------"
echo "Resetting last context state of affected datasets client."
echo "-----------------------------------------------------------"
query_affected_datasets --reset

echo "-----------------------------------------------------------"
echo "Restarting cron activities."
echo "-----------------------------------------------------------"
cd ${CRDS}/CRDS_server
crontab <host/crontab

