#! /bin/csh

server
source env.csh

if ($#argv < 2) then
    echo "usage: `basename $0` <old_context> <new_context> [fast]"
    exit 1
endif

set old_context = $1;  shift;
set new_context = $1;  shift;

if ($1 == "fast") then
    shift;
    set switches = "--diffs-only --datasets-since=auto"
else
    set switches = "--all-instruments"
endif

set date = `datetag`
set subject = "CRDS $CRDS_PROJECT $CRDS_USECASE Datasets Affected by Change from $old_context to $new_context on $date"

# rip out the ints so csh < works,  e.g. hst_0200.pmap --> 0200
set old = `echo ${old_context} | cut -d'_' -f2`
set old = `echo ${old} | cut -d'.' -f1`  
set new = `echo ${new_context} | cut -d'_' -f2`
set new = `echo ${new} | cut -d'.' -f1`
set output_dir = "${CRDS}/monitor_reprocessing/`datetag`_${old}_${new}"
mkdir -p $output_dir
cd $output_dir

echo >bestrefs_err.txt
(   echo "--------------------------------------------------------------------------------------------------------------"; \
    echo $subject; \
    echo "--------------------------------------------------------------------------------------------------------------"; \
    python -m crds.bestrefs --old-context $old_context --new-context $new_context --print-affected --stats $switches $* >affected_ids.txt; \
    echo $status >bestrefs.status; \
) |& tee bestrefs_err.txt

setenv bestrefs_status `cat bestrefs.status`

if ($bestrefs_status == 0) then
    set disposition="OK"
else if ($bestrefs_status == 1) then
    set disposition="ERRORS"
else
    set disposition="FAILED"
endif

(echo "--------------------------------------------------------------------------------------------------------------"; \
 echo "${disposition}: Context switch from $old_context to $new_context affected `wc -l affected_ids.txt | cut -d' ' -f1` datasets"; \
 echo "--------------------------------------------------------------------------------------------------------------"; \
) |& tee -a bestrefs_err.txt

mail -a`pwd`/bestrefs_err.txt -a`pwd`/affected_ids.txt -s"${disposition}: $subject" $CRDS_AFFECTED_DATASETS_RECIPIENTS </dev/null
