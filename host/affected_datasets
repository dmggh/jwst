#! /bin/csh

server
source env.csh

if ($#argv < 2) then
    echo "usage: `basename $0` <old_context> <new_context> [fast]"
    exit 1
endif

set old_context = $1;  shift;
set new_context = $1;  shift;

if ($1 == "fast") then
    shift;
    set switches = "--diffs-only --datasets-since=auto"
else
    set switches = "--all-instruments"
endif

set date = `datetag`
set header = "CRDS $CRDS_PROJECT $CRDS_USECASE datasets affected $old_context --> $new_context on $date"

# rip out the ints so csh < works,  e.g. hst_0200.pmap --> 0200
set old = `echo ${old_context} | cut -d'_' -f2`
set old = `echo ${old} | cut -d'.' -f1`  
set new = `echo ${new_context} | cut -d'_' -f2`
set new = `echo ${new} | cut -d'.' -f1`
set output_dir = "${CRDS}/monitor_reprocessing/`datetag`_${old}_${new}"
mkdir -p $output_dir
cd $output_dir

echo >bestrefs_err.txt
(   echo "--------------------------------------------------------------------------------------------------------------"; \
    echo "$header"; \
    echo "--------------------------------------------------------------------------------------------------------------"; \
    python -m crds.bestrefs --old-context $old_context --new-context $new_context --print-affected \
	--stats --print-update-counts --dump-unique-errors $switches $* >affected_ids.txt; \
    echo $status >bestrefs.status; \
) |& tee bestrefs_err.txt

setenv bestrefs_status `cat bestrefs.status`

if ($bestrefs_status == 0) then
    set disposition="OK"
else if ($bestrefs_status == 1) then
    set disposition="ERRORS"
else
    set disposition="FAILED"
endif

set affected_count=`wc -l affected_ids.txt | cut -d' ' -f1`

set subject="${disposition}: $header : $affected_count affected"

(echo "--------------------------------------------------------------------------------------------------------------"; \
 echo "$subject"; \
 echo "--------------------------------------------------------------------------------------------------------------"; \
) |& tee -a bestrefs_err.txt

# When things go really badly, error logs can approach a gigabyte,  enough to gag thunderbird even if
# the mailer will actually send the attachment.  So,  instead,  create a truncated error file.

if (`wc -l bestrefs_err.txt | cut -d' ' -f1` < 400) then
    set bestrefs_err_file="bestrefs_err.txt"
else
    set bestrefs_err_file="bestrefs_err_truncated.txt"
    head -n 200 bestrefs_err.txt >bestrefs_err_truncated.txt
    echo ".... <snip> ...." >>bestrefs_err_truncated.txt
    tail -n 200 bestrefs_err.txt >>bestrefs_err_truncated.txt
endif

gzip affected_ids.txt

mail -r crds@stsci.edu -a`pwd`/$bestrefs_err_file -a`pwd`/affected_ids.txt.gz -s "$subject" $CRDS_AFFECTED_DATASETS_RECIPIENTS </dev/null

