#! /bin/tcsh

# This script verifies the shared readonly cache using sha1sums.  It's intended
# to be run as a cronjob weekly or less,  only on the DEV server since it will
# saturate the VM file buffer cache and degrade CRDS server performance.

server
source env.csh

if ( $1 == "shared" ) then
    shift;
    if (("${CRDS_USECASE}" != "dev") && ("${CRDS_USECASE}" != "django")) then
	echo "Skipping sync_verify_cache on ${CRDS_PROJECT} ${CRDS_USECASE} server."
	exit 0
    endif
    echo "================================================================================================="
    echo "Verifying global shared readonly cache."
    setenv CRDS_PATH /grp/crds/${CRDS_PROJECT}
    setenv CRDS_SERVER_URL https://${CRDS_PROJECT}-crds.stsci.edu
else if ( $1 == "local" ) then
    shift;
    echo "================================================================================================="
    echo "Verifying local cache."    
else
    echo "usage: crds_verify_cache [local|shared] [<extra sync parameters>]"
    exit -1
endif

if ($#argv < 1) then
    set cmd_params="--all --check-sha1sum --fetch-references --readonly-cache --verbose"
else
    set cmd_params="$*"
endif

echo "================================================================================================="
echo "CRDS Configuration for Cache Verification on ${CRDS_USECASE} VM"
echo "================================================================================================="
python -m crds.list --config

echo "================================================================================================="
echo "Verifying cache using crds.sync"
echo "================================================================================================="
python -m crds.sync --verbose --stats  --profile=console $cmd_params

